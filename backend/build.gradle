plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '8.2.0'
    id "checkstyle"
    id "pmd"
    id "com.github.spotbugs" version "6.0.22"
}

group = 'ch.gibb'
version = '0.0.0'

checkstyle {
    toolVersion = "10.17.0"
    configFile = rootProject.file("checkstyle.xml")
}

spotbugs {
    ignoreFailures = false
}



tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

pmd {
    toolVersion = "7.6.0"
    consoleOutput = true
    ignoreFailures = false
}

tasks.named("check") {
    dependsOn("spotbugsMain", "spotbugsTest")
    dependsOn("checkstyleMain", "checkstyleTest", "pmdMain", "pmdTest")
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		endWithNewline()
	}
	java {
		importOrder()
		removeUnusedImports()
		cleanthat()
		googleJavaFormat().aosp().reflowLongStrings().skipJavadocFormatting()
		formatAnnotations()
	}
}
